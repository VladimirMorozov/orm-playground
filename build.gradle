plugins {
    id 'nu.studer.jooq' version '3.0.3'
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'me.vmorozov'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

ext['jooq.version'] = jooqVersion
jooq {
    version = jooqVersion // seems to be ignored because of io.spring.dependency-management
    edition = 'OSS'
    ormPlayground(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://192.168.99.100:5432/postgres'
            user = 'postgres'
            password = 'secret'
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'me.vmorozov.orm.playground.jooq.generator.CustomGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                pojos = true
                fluentSetters = true
                javaTimeTypes = true
                validationAnnotations = true
                // ...
            }
            target {
                packageName = 'me.vmorozov.orm.playground.jooq.generated'
                directory = 'src/main/java/'
            }
        }
    }
}
project.tasks.getByName('compileJava').dependsOn -= 'generateOrmPlaygroundJooqSchemaSource' // compileJava shouldn't require a db


dependencies {

    // while I don't need this starter directly, 
    // serialization for web is an important part of mapping,
    // I need auto-configured objectMapper from it
    implementation("org.springframework.boot:spring-boot-starter-web")

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hibernate5', version: '2.9.8'
    
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.0.0'
    implementation("org.mybatis:mybatis-typehandlers-jsr310:1.0.2")

    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation group: 'org.simpleflatmapper', name: 'sfm-jooq', version: '6.4.2'
    jooqRuntime 'org.postgresql:postgresql'
    jooqRuntime project(':orm.playground.jooq.generator')

    runtimeOnly 'org.postgresql:postgresql'
    implementation('org.hibernate.validator:hibernate-validator:6.0.14.Final')
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation('org.assertj:assertj-core:3.11.0')

    //implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
    
    

    
}
